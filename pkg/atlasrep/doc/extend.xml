
<!-- %W  extend.xml      GAP 4 package AtlasRep             Thomas Breuer -->
<!-- %H  $Id: extend.xml,v 1.8 2008/11/28 09:00:29 gap Exp $ -->
<!-- %Y  Copyright 2002, Lehrstuhl D für Mathematik, RWTH Aachen, Germany -->


<!-- %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% -->
<Chapter Label="chap:Private Extensions">
<Heading>Private Extensions of the <Package>AtlasRep</Package> Package
</Heading>

It may be interesting to use the functions of the &GAP; interface also
for representations or programs that are <E>not</E> part of the
&ATLAS; of Group Representations.
This chapter describes how to achieve this.

<P/>

The main idea is that users can notify directories containing the
<Q>private</Q> data files, which may consist of

<Enum>
<Item>
  new representations and programs for groups
  that are declared already in the <Q>official</Q>
  &ATLAS; of Group Representations,
</Item>
<Item>
  the declaration of groups that are not declared in the <Q>official</Q>
  &ATLAS; of Group Representations,
  and representations and programs for them, and
</Item>
<Item>
  the definition of new kinds of representations and programs.
</Item>
</Enum>

<P/>

The first two kinds are dealt with in
Section&nbsp;<Ref Sect="sect:Adding a Private Data Directory"/>
and Section&nbsp;<Ref Sect="sect:Effect of Private Extensions"/>.
The last is described in
Section&nbsp;<Ref Sect="sect:Data Types Used in the AGR"/>.

<P/>

Finally, an example of using private extensions is given in
Section&nbsp;<Ref Sect="sect:An Example of Extending AtlasRep"/>.

<P/>

Several of the sanity checks for the official part of the
<Package>AtlasRep</Package> package make sense also for private extensions,
see Section&nbsp;<Ref Sect="sect:Sanity Checks for the Atlas of Group Representations"/>
for more information.


<!-- %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% -->
<Section Label="sect:Adding a Private Data Directory">
<Heading>Adding a Private Data Directory</Heading>

<#Include Label="[3]{access}">

<P/>

<#Include Label="AtlasOfGroupRepresentationsNotifyPrivateDirectory">

<P/>

<#Include Label="AtlasOfGroupRepresentationsForgetPrivateDirectory">

</Section>


<!-- %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% -->
<Section Label="sect:Effect of Private Extensions">
<Heading>The Effect of Private Extensions on the User Interface</Heading>

First suppose that only new groups or new data for known groups are added.

<P/>

In this case, <Ref Func="DisplayAtlasInfo"/> lists the private
representations and programs in the same way as the
<Q>official</Q> data, except that private parts are marked with the string
stored in the component <C>markprivate</C> of
<Ref Var="AtlasOfGroupRepresentationsInfo"/>;
by default, this is a star <C>*</C>.
The ordering of representations listed by <Ref Func="DisplayAtlasInfo"/>
(and referred to by <Ref Func="AtlasGenerators"/>) will in general change
when private directories are notified.
If several private directories are used then the ordering of data may depend
on the ordering of notifications.
For the other interface functions described in
Chapter&nbsp;<Ref Chap="chap:The User Interface of the AtlasRep Package"/>,
the only difference is that also the private data can be accessed.
In particular the <Q>free format</Q>
<C><A>groupname</A>G<A>i</A>-X<A>descr</A>W<A>n</A></C>
for straight line programs
(see Section&nbsp;<Ref Sect="sect:Filenames Used in the AGR"/>)
may be used in private directories;
the data can be accessed with <Ref Func="AtlasProgram"/>,
where the last two arguments are the strings <C>"other"</C> and <A>descr</A>.

<P/>

If also private data types are introduced
(see Section&nbsp;<Ref Sect="sect:Data Types Used in the AGR"/>)
then additional columns or rows can appear in the output of
<Ref Func="DisplayAtlasInfo"/>,
and new inputs can become meaningful for all interface functions.
Examples for these changes can be found in
Section&nbsp;<Ref Sect="sect:An Example of Extending AtlasRep"/>.

</Section>


<!-- %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% -->
<Section Label="sect:An Example of Extending AtlasRep">
<Heading>An Example of Extending the <Package>AtlasRep</Package> Package
</Heading>

In the beginning we set the info level of <Ref InfoClass="InfoAtlasRep"/>
to <M>1</M>.

<Example><![CDATA[
gap> level:= InfoLevel( InfoAtlasRep );;
gap> SetInfoLevel( InfoAtlasRep, 1 );
]]></Example>

Let us assume that the directory <F>privdir</F> contains
data for the cyclic group <M>C_4</M> of order <M>4</M>
and for the symmetric group <M>S_5</M> on <M>5</M> points, respectively.
Note that it is obvious what the term <Q>standard generators</Q> means for
the group <M>C_4</M>.

<P/>

Further let us assume that <F>privdir</F> contains the following files.

<List>
<Mark><F>C4G1-p4B0.m1</F></Mark>
<Item>
    a faithful permutation representation of <M>C_4</M> on <M>4</M> points,
</Item>
<Mark><F>C4G1-max1W1</F></Mark>
<Item>
    the straight line program that returns the square of its unique input,
</Item>
<Mark><F>C4G1-a2W1</F></Mark>
<Item>
    the straight line program that applies an outer automorphism to its
    unique input,
</Item>
<Mark><F>C4G1-XtestW1</F></Mark>
<Item>
    the straight line program that returns the square of its unique input,
</Item>
<Mark><F>S5G1-p2B0.m1</F> and <F>S5G1-p2B0.m2</F></Mark>
<Item>
    the permutation representation of the commutator factor group
    of <M>S_5</M>, on <M>2</M> points.
</Item>
</List>

<P/>

The directory and the files can be created as follows.

<P/>

<Example><![CDATA[
gap> pkg:= Filename( DirectoriesPackageLibrary( "atlasrep", "" ), "" );;
gap> prv:= DirectoryTemporary( "privdir" );;
gap> FileString( Filename( prv, "C4G1-p4B0.m1" ),
>                MeatAxeString( [ (1,2,3,4) ], 4 ) );;
gap> FileString( Filename( prv, "C4G1-max1W1" ),
>                "inp 1\npwr 2 1 2\noup 1 2\n" );;
gap> FileString( Filename( prv, "C4G1-XtestW1" ),
>                "inp 1\npwr 2 1 2\noup 1 2\n" );;
gap> FileString( Filename( prv, "C4G1-a2W1" ),
>                "inp 1\npwr 3 1 2\noup 1 2\n" );;
gap> FileString( Filename( prv, "C4G1-Ar1aB0.g" ),
>                "return rec( generators:= [ [[E(4)]] ] );\n" );;
gap> FileString( Filename( prv, "S5G1-p2B0.m1" ),
>                MeatAxeString( [ (1,2) ], 2 ) );;
gap> FileString( Filename( prv, "S5G1-p2B0.m2" ),
>                MeatAxeString( [ (1,2) ], 2 ) );;
]]></Example>

<P/>

(We could also introduce intermediate directories <F>C4</F> and <F>S5</F>,
say, each with the data for one group only.
Here we do not show this because creating directories seems to be not
possible without the &GAP; package <Package>IO</Package>.)

<P/>

Now we create a file whose name is not admissible
because it occurs already in the official part of the database.
(This will cause an info line below.)

<P/>

<Example><![CDATA[
gap> FileString( Filename( prv, "S5G1-p5B0.m1" ),
>                MeatAxeString( [ (1,2) ], 5 ) );;
]]></Example>

<P/>

The official part of the <Package>AtlasRep</Package> package does not contain
information about <M>C_4</M>,
so we first notify this group, in the file <F>privdir/toc.g</F>.
(The group <M>S_5</M> is known with name <C>A5.2</C> in the official part
of the <Package>AtlasRep</Package> package, so it cannot be notified.)

<P/>

<Example><![CDATA[
gap> FileString( Filename( prv, "toc.g" ),
>                "AGRGNAN(\"C4\",\"C4\");\n" );;
]]></Example>

<P/>

Then we notify the private directory.

<P/>

<Example><![CDATA[
gap> AtlasOfGroupRepresentationsNotifyPrivateDirectory( prv, "priv" );
#I  file `S5G1-p5B0.m1' was already in another t.o.c.
false
]]></Example>

<P/>

(The nonadmissible filename <C>S5G1-p5B0.m1</C> was ignored for the private
directory.
If this file would not be present then the return value would be
<K>true</K>.)

Now we can use the interface functions for accessing the data in the
private directory.

<P/>

<Example><![CDATA[
gap> DisplayAtlasInfo( [ "C4" ] );
group    #  maxes  cl  cyc  out  find  check  pres
--------------------------------------------------
C4*      2      1   -    -    2     -      -     -
gap> DisplayAtlasInfo( "C4" );
Representations for G = C4:    (all refer to std. generators 1)
---------------------------
1: G <= Sym(4)*
2: G <= GL(1a,C)*

Programs for G = C4:    (all refer to std. generators 1)
--------------------
available maxes of G:  [ 1 ]*
available automorphisms:  [ "2" ]*
available other scripts:
   "test"*
gap> DisplayAtlasInfo( "C4", IsPermGroup, true );
Representations for G = C4:    (all refer to std. generators 1)
---------------------------
1: G <= Sym(4)*
gap> DisplayAtlasInfo( "C4", IsMatrixGroup );
Representations for G = C4:    (all refer to std. generators 1)
---------------------------
2: G <= GL(1a,C)*
gap> DisplayAtlasInfo( "C4", Dimension, 2 );
gap> DisplayAtlasInfo( "A5.2", NrMovedPoints, 2 );
Representations for G = A5.2:    (all refer to std. generators 1)
-----------------------------
1: G <= Sym(2)*
gap> info:= OneAtlasGeneratingSetInfo( "C4" );
rec( groupname := "C4", standardization := 1, 
  identifier := [ [ "priv", "C4" ], [ "C4G1-p4B0.m1" ], 1, 4 ], p := 4, 
  id := "", repnr := 1 )
gap> AtlasGenerators( info.identifier );
rec( generators := [ (1,2,3,4) ], groupname := "C4", standardization := 1,
  repnr := 1, identifier := [ [ "priv", "C4" ], [ "C4G1-p4B0.m1" ], 1, 4 ], 
  p := 4, id := "" )
gap> AtlasProgram( "C4", 1 );
rec( program := <straight line program>, standardization := 1, 
  identifier := [ [ "priv", "C4" ], "C4G1-max1W1", 1 ], groupname := "C4" )
gap> AtlasProgram( "C4", "maxes", 1 );
rec( program := <straight line program>, standardization := 1, 
  identifier := [ [ "priv", "C4" ], "C4G1-max1W1", 1 ], groupname := "C4" )
gap> AtlasProgram( "C4", "maxes", 2 );
fail
gap> AtlasGenerators( "C4", 1 );
rec( generators := [ (1,2,3,4) ], groupname := "C4", standardization := 1,
  repnr := 1, identifier := [ [ "priv", "C4" ], [ "C4G1-p4B0.m1" ], 1, 4 ], 
  p := 4, id := "" )
gap> AtlasGenerators( "C4", 2 );
rec( generators := [ [ [ E(4) ] ] ], groupname := "C4", standardization := 1,
  repnr := 2, identifier := [ [ "priv", "C4" ], "C4G1-Ar1aB0.g", 1, 1 ], 
  dim := 1, id := "a" )
gap> AtlasGenerators( "C4", 3 );
fail
gap> AtlasProgram( "C4", "other", "test" );
rec( program := <straight line program>, standardization := 1, 
  identifier := [ [ "priv", "C4" ], "C4G1-XtestW1", 1 ], groupname := "C4" )
]]></Example>

<P/>

For checking the data in the private directory,
we apply some of the sanity checks
(see Section&nbsp;<Ref Sect="sect:Sanity Checks for the Atlas of Group Representations"/>).

<P/>

<Example><![CDATA[
gap> AtlasOfGroupRepresentationsTestWords( "priv" );
true
gap> AtlasOfGroupRepresentationsTestFileHeaders( "priv" );
true
]]></Example>

<P/>

Finally, we <Q>uninstall</Q> the private directory,
and reset the info level that had been set to <M>1</M> in the beginning.
(Also the group name <C>C4</C> is removed this way, which is an advantage of
using a <F>toc.g</F> file over calling <Ref Func="AGRGNAN"/> directly.),
Note that we need not remove the data in the temporary directory,
&GAP; will do this automatically.

<P/>

<Example><![CDATA[
gap> AtlasOfGroupRepresentationsForgetPrivateDirectory( "priv" );
gap> SetInfoLevel( InfoAtlasRep, level );
]]></Example>

</Section>


<!-- %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% -->

</Chapter>

