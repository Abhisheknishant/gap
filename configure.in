AC_INIT(configure)

if test "x$CFLAGS" = "x" ;  then
  CFLAGS=${COPTS}
fi

AC_CHECK_SIZEOF(void *, 4)

AC_ARG_WITH(abi,
 AC_HELP_STRING([--with-abi],
                [Set this equal to 32 or 64 to build GAP (and GMP provided you
 		 do not deselect it) in 32- or 64-bit mode. The default value
		 for this option is determined by testing the behaviour of your
		 compiler, so should be 32 on a 32-bit system and 64 on one
		 which is 64-bit.]),
[  if test "$withval" = 64; then
    if test $ac_cv_sizeof_void_p = 4; then
     AC_MSG_ERROR([--with-abi=64 cannot be used on a 32-bit system.
                    Please replace this value with 32 and retry configure.
                    If you think this error is wrong, email
                    support@gap-system.org with full details.])
    else
     BUILD_MODE="64"
     ABI_CFLAGS="-m64"
    fi
   elif test "$withval" = 32; then
    BUILD_MODE="32"
    ABI_CFLAGS="-m32"
   elif ! test "$withval" = ""; then
    AC_MSG_ERROR([Unrecognised build mode "$withval". Please specify 32 or 64,
                  or do not set this option.])
   else
    if test $ac_cv_sizeof_void_p = 8; then
     BUILD_MODE="64"
    else
     BUILD_MODE="32"
    fi
    ABI_CFLAGS=""
   fi
],
[
 if test $ac_cv_sizeof_void_p = 8; then
  BUILD_MODE="64"
 else
  BUILD_MODE="32"
 fi
 ABI_CFLAGS=""
]
)

AC_SUBST(BUILD_MODE)
AC_SUBST(ABI_CFLAGS)

gp_configure_options=$ac_configure_args
AC_SUBST(gp_configure_options)
AC_CONFIG_AUX_DIR(cnf)
AC_CANONICAL_TARGET
AC_PROG_CC
BASECC=`basename ${CC}`
AC_SUBST(BASECC)
AC_PROG_MAKE_SET
AC_SUBST(gapdir)
gapdir=`pwd`

AC_ARG_WITH(gmp,
 AC_HELP_STRING( [--with-gmp],
  [ Use GMP library.
    If the argument you supply is "yes" or <empty>, then the version of GMP bundled with this GAP will be used. (Default)
    If the argument is "system" that means the library is reachable with the standard
    search path "/usr" or "/usr/local".
    Otherwise you give the <path> to the directory which contains the library. 
    If the argument is no, use original GAP large integers instead of GMP.
    [[default=yes]]
  ]),

 [ if test "$withval" = yes ; then
    GMP_HOME="`pwd`/bin/$target-$BASECC/${BUILD_MODE}-bit/extern/gmp"
    GMP_CFLAGS="-DUSE_GMP -I${GMP_HOME}/include"
    GMP_LIBS="${GMP_HOME}/lib/libgmp.a"
    MAKE_GMP="gmp"
    GMP_VER="5.0.1"
   elif test "$withval" = system ; then
    GMP_CFLAGS="-DUSE_GMP -m${BUILD_MODE}"
    GMP_LIBS="-lgmp"
    MAKE_GMP=""
   elif test "$withval" != no ; then
    GMP_HOME="$withval"
    GMP_CFLAGS="-DUSE_GMP -I${GMP_HOME}/include"
    GMP_LIBS="${GMP_HOME}/lib/libgmp.a"
    MAKE_GMP=""
   else
    GMP_CFLAGS=""
    GMP_LIBS=""
    MAKE_GMP=""
   fi
 ],
 [ GMP_HOME="`pwd`/bin/$target-$BASECC/${BUILD_MODE}-bit/extern/gmp"
   GMP_CFLAGS="-DUSE_GMP -I${GMP_HOME}/include"
   GMP_LIBS="${GMP_HOME}/lib/libgmp.a"
   MAKE_GMP="gmp"
   GMP_VER="5.0.1"
 ]
)

AC_ARG_WITH(readline,
 [  --with-readline=yes|no|<path>
    Use readline library for command line editing.
 ],
 [  if test "$withval" = "no"; then
      NOREADLINE="-DNO_READLINE"
      READLINEPREFIX=""
    elif test "$withval" = "yes"; then
      NOREADLINE=""
      READLINEPREFIX=""
    else
      NOREADLINE=""
      READLINEPREFIX=$withval
    fi
 ],
 []
)

JENKINS_CFLAGS="-I`pwd`/bin/$target-$BASECC/${BUILD_MODE}-bit/extern/jenkins"
AC_SUBST(JENKINS_CFLAGS)
AC_SUBST(GMP_CFLAGS)
AC_SUBST(GMP_LIBS)
AC_SUBST(MAKE_GMP)
AC_SUBST(GMP_VER)
AC_SUBST(NOREADLINE)
AC_SUBST(READLINEPREFIX)

mkdir -p bin
AC_OUTPUT(Makefile sysinfo.gap:sysinfo.in bin/gap${BUILD_MODE}.sh:gap.shi)
ln -sf gap${BUILD_MODE}.sh bin/gap.sh
